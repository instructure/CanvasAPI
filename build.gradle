repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.13.2'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'robolectric'

dependencies {
    compile('com.mobprofs:retrofit-simplexmlconverter:1.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.8.0'

    compile group: 'org.apache.httpcomponents' , name: 'httpclient-android' , version: '4.3.5'
    compile (group: 'org.apache.httpcomponents' , name: 'httpmime' , version: '4.3.5') {
        exclude module: 'org.apache.httpcomponents:httpclient'
    }

    compile 'com.netflix.rxjava:rxjava-android:0.20.6'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.robolectric:robolectric:2.3'
    androidTestCompile 'com.squareup:fest-android:1.0.3'
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    productFlavors {
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    sourceSets {
        androidTest.setRoot('src/test')
    }
}

//Do some maven stuff
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.2.0"
group = "com.instructure"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        //Remove everything below this for maven builds.
        //repository(url: sonatypeRepo) {
        //    authentication(userName: sonatypeUsername,
        //            password: sonatypePassword)
        //}
        //Remove everything above this for maven builds.

        pom.project {
            name 'CanvasAPI'
            packaging 'aar'
            description 'CanvasAPI is a library that will help you integrate your own third party app with Canvas by Instructure.'
            url 'https://github.com/instructure/CanvasAPI'

            scm {
                url 'git@github.com:instructure/CanvasAPI.git'
                connection 'scm:git:git@github.com:instructure/CanvasAPI.git'
                developerConnection 'scm:git:git@github.com:instructure/CanvasAPI.git'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'tneedham'
                    name 'Trevor Needham'
                    email 'tneedham@instructure.com'
                }
                developer {
                    id 'brady'
                    name 'Brady Larson'
                    email 'brady@instructure.com'
                }
                developer {
                    id 'hhoang'
                    name 'Hoa Hoang'
                    email 'hhoang@instructure.com'
                }
                developer {
                    id 'mrice'
                    name 'Matthew Rice'
                    email 'mrice@instructure.com'
                }
                developer {
                    id 'dwilloughby'
                    name 'Dan Willoughby'
                    email 'dwilloughby@instructure.com'
                }
            }
        }
    }
}
